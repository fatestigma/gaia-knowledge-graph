insert {
    [] a rdf:Statement ;
        rdf:subject ?prototype ;
        rdf:predicate rdf:type ;
        rdf:object ?t
    }
where {
    select ?prototype (max(?type1) as ?t)
    where {
      {
        select ?prototype (max(?cnt) as ?max)
        where {
          select ?prototype ?type (count(?type) as ?cnt)
          where {
            ?c aida:prototype ?prototype .
            ?x a rdf:Statement ; rdf:subject ?e ; rdf:predicate rdf:type ; rdf:object ?type .
            ?membership aida:cluster ?c ; aida:clusterMember ?e .
          } group by ?prototype ?type
        } group by ?prototype
      }
      {
        select ?prototype ?type1 (count(?type1) as ?max)
        where {
            ?c1 aida:prototype ?prototype .
            ?x a rdf:Statement ; rdf:subject ?e1 ; rdf:predicate rdf:type ; rdf:object ?type1 .
            ?membership1 aida:cluster ?c1 ; aida:clusterMember ?e1 .
        } group by ?prototype ?type1
      }
    } group by ?prototype
}
